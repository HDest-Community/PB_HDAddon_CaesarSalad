// ------------------------------------------------------------
// Former Human
// ------------------------------------------------------------

class PB_ACRZombie:HDHumanoid{
	default{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Zombieman (Flechette Rifle)"
		//$Sprite "CRZMA0"

		seesound "grunt/sight";
		painsound "grunt/pain";
		deathsound "grunt/death";
		activesound "grunt/active";
		tag "Undead Contractor";

		health 85;
		speed 8;
		painchance 200;
		obituary "%o was given an acupuncture.";
		hitobituary "%o felt some pins and needles.";
	}
	double turnamount;
	int user_weapon;
	int mag;
	int firemode; // -1 semi only; 0 semi; 1 auto;
	bool aiming;

	override void postbeginplay(){
		super.postbeginplay();
		
		bhashelmet;
		givearmour(1.,0.,-0.4); //no chance to spawn with blue armor
		
		if(user_weapon)accuracy=user_weapon;
		if(accuracy==1)firemode=random(0,3);
		else if(accuracy==2)firemode=-1;
		else firemode=random(-1,3);
		if(firemode==-1){
			mag=random(1,42);
		}else{
			mag=random(1,42);
			if(firemode>2)firemode=1;
			maxtargetrange=2500;
		}
	}
	
	override void tick()
	{
		super.tick();
		
		if(instatesequence(curstate,resolvestate("aiming.lr"))
		||instatesequence(curstate,resolvestate("aiming"))
		||instatesequence(curstate,resolvestate("shoot.lr"))
		||instatesequence(curstate,resolvestate("shoot"))
		)
		{
			A_LaserSight();
			if(!isfrozen())
			{
				A_SetAngle(angle+frandom(-0.075,0.075),SPF_INTERPOLATE);
				A_SetPitch(pitch+frandom(-0.075,0.075),SPF_INTERPOLATE);
			}
		}
	}

	action void A_LaserSight()
    {
        FLineTraceData trace;
		double rtt=0.03*trace.distance;
		double saf=1.;
		let hdm=hdmobbase(self);
		
		//A_CustomRailgun(0,0,"","Red",RGF_FULLBRIGHT|RGF_SILENT|RGF_NOPIERCING|RGF_CENTERZ
		//				,0,1,"ACRScanDot",0,0,0,1,0.1,0,null,(hdm.gunheight)*0.5,limit:1);
		
				linetrace(
					hdm.angle,HDCONST_ONEMETRE*2500,hdm.pitch,
					flags:TRF_NOSKY|TRF_ABSOFFSET,
					offsetz:hdm.gunheight,
					data:trace
				);
		
        Vector3 laserOfs = trace.hitLocation-pos;
		laserOfs-=trace.hitdir;
		
				if(
			trace.hittype==Trace_HitNone
			||(
				trace.hitline&&(
					trace.hitline.special==Line_Horizon
					||(
						trace.linepart==2
						&&trace.hitsector.gettexture(0)==skyflatnum
					)||(
						trace.linepart==1
						&&trace.hitline.sidedef[1]
						&&hdmath.oppositesector(trace.hitline,trace.hitsector).gettexture(0)==skyflatnum
					)
				)
			)
		)return;
		
		a_spawnparticleex(
						"ff 00 00",TexMan.CheckForTexture("lasfx000"),STYLE_ADD,SPF_FULLBRIGHT|SPF_RELVEL|SPF_NOTIMEFREEZE,
						lifetime:1,frandom(3.1,3.5),0,
						laserOfs.x,
						laserOfs.y,
						laserOfs.z,
						startalphaf:1
					);
		a_spawnparticleex(
						"ff 00 00",TexMan.CheckForTexture("lasfx000"),STYLE_ADD,SPF_FULLBRIGHT|SPF_RELVEL|SPF_NOTIMEFREEZE,
						lifetime:4,frandom(4.1,4.9),0,
						laserOfs.x,
						laserOfs.y,
						laserOfs.z,
						startalphaf:0.5,0.1,-1.5
					);
		
    }

	void A_ZomFrag(){
		bool garbage;actor gg;
		double cpp=cos(pitch);double spp=sin(pitch);
		[garbage,gg]=A_SpawnItemEx("HDFragSpoon",
			cpp*-4,-3,height-6-spp*-4,
			cpp*3,0,-spp*3,
			frandom(30,45),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
		);
		gg.vel+=self.vel;
		double gforce=frandom(10,30);
		[garbage,gg]=A_SpawnItemEx("HDFragGrenade",
			0,0,height-6,
			cpp*gforce,0,-spp*gforce,
			0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
		);
		gg.vel+=self.vel;
	}
	
	void A_EjectACRCasing(){
		HDWeapon.EjectCasing(self,"HDSpent6mmFlechette",
			frandom(-0.5,0.5),
			(frandom(0,1),0,frandom(0,1)),
			(2,0,0)
		);
	}
	
	override void deathdrop(){
		if(bhasdropped&&bfriendly)return;
		hdweapon wp=null;{
			if(!bhasdropped){
				if(wp=DropNewWeapon("HD_SteyrACR")){
					(random(0,1)*SACRS_CHAMBER);
					wp.weaponstatus[SACRS_MAG]=mag;
					if(firemode==-1)wp.weaponstatus[0];
				}
				DropNewItem("HDFragGrenadeAmmo",42);
				if (!random(0, 8))DropNewWeapon("HDCombatKnife");
			}else{
				DropNewItem("PB_HDSteyrACRMag",96);
			}
		}
		if(!bhasdropped){
			DropNewItem("HDHandgunRandomDrop");
			bhasdropped=true;
		}
	}
	
	//post-shot checks
	void A_HDMonsterRefire(statelabel jumpto,int chancetocontinue=0){
		if(
			random(1,100)>chancetocontinue
			&&(
				!target
				||!checksight(target)
				||target.health<1
				||absangle(angle,angleto(target))>3
				||!hdmobai.tryshoot(self,flags:hdmobai.TS_GEOMETRYOK)
			)
		)setstatelabel(jumpto);
	}
	
	states{
	spawn:
		CRZM A 0;
	idle:
	spawn2:
		#### A 0{
			A_HDLook();
			A_Recoil(frandom(-0.1,0.1));
		}
		#### EEE 1{
			A_SetTics(random(5,17));
			A_HDLook();
		}
		#### E 1{
			A_Recoil(frandom(-0.1,0.1));
			A_SetTics(random(10,40));
		}
		#### B 0 A_Jump(28,"spawngrunt");
		#### B 0 A_Jump(132,"spawnswitch");
		#### B 8 A_Recoil(frandom(-0.2,0.2));
		loop;
	spawngrunt:
		#### G 1{
			A_Recoil(frandom(-0.4,0.4));
			A_SetTics(random(30,80));
			if(!random(0,7))A_Vocalize(activesound);
		}
		#### A 0 A_Jump(256,"spawn2");
	spawnswitch:
		#### A 0 A_JumpIf(bambush,"spawnstill");
		goto spawnwander;
	spawnstill:
		#### A 0 A_HDLook();
		#### A 0 A_Recoil(random(-1,1)*0.4);
		#### CD 5 A_SetAngle(angle+random(-4,4));
		#### A 0{
			A_HDLook();
			if(!random(0,127))A_Vocalize(activesound);
		}
		#### AB 5 A_SetAngle(angle+random(-4,4));
		#### B 1 A_SetTics(random(10,40));
		#### A 0 A_Jump(256,"spawn2");
	spawnwander:
		CRZM A 0 A_HDLook();
		#### CD 5 A_Wander();
		#### AB 5{
			A_HDLook();
			if(!random(0,15))A_Vocalize(activesound);
			A_Wander();
		}
		#### A 0 A_Jump(196,"spawn");
		loop;
	missile:
		#### A 0{
			if(!target){
				setstatelabel("spawn2");
				return;
			}
			double dt=distance3d(target);
			/*if(
				target
				&&!random(0,12)
				&&dt>600
				&&dt<1000
			)setstatelabel("frag");*/
		}
	missile2:
		#### ABCD 3 A_TurnToAim(40,shootstate:"aiming");
		loop;
	aiming:
		#### E 0
		{
			double dt=distance3d(target);
			if(
				target
				&&dt>300
			)setstatelabel("aiming.lr");
		}
		#### E 5 {A_FaceLastTargetPos(30);A_Vocalize(seesound);}
		#### E 1 A_StartAim(maxspread:20,maxtics:random(0,35));
		//fallthrough to shoot
	shoot:
		#### F 1 A_SetTics(min(1,int(lasttargetdist)>>5));
		#### E 0{
			pitch+=frandom(-spread,spread);
			angle+=frandom(-spread,spread);
			
			A_LeadTarget(lasttargetdist*0.01,randompick(0,0,0,1));
		}
	fire:
		#### F 1 bright light("SHOT"){
			if(mag<1){
				setstatelabel("ohforfuckssake");
				return;
			}else{
				A_StartSound("weapons/steyracr/fire",CHAN_WEAPON);
				HDBulletActor.FireBullet(self,"HDB_6mmFlechette",spread:1.85,speedfactor:0.9);
				A_EjectACRCasing();
				
				pitch+=frandom(-0.2,0.1)*spread;
				angle+=frandom(-0.2,0.1)*spread;
				mag--;}
		}
		#### E 1{spread++;}
		#### E random(5,8) A_FaceLastTargetPos(80);
		#### E 0 A_Jump(128,"fire");
	postshot:
		#### E 12{
			if(!random(0,127))A_Vocalize(activesound);
			if(mag<1){
				setstatelabel("reload");
				return;
			}
			spread=max(0,spread-1);
			A_SetTics(random(2,6));
		}
		#### E 3;
		#### E 0 A_JumpIf(!hdmobai.tryshoot(self),"see");
		#### E 0 A_JumpIfTargetInLOS(1);
		goto coverfire;  //even if not in los,occasionally keep shooting anyway
		#### E 3 A_FaceTarget(10,10);
		#### E 0 A_Jump(50,"see");  //even if in los,occasionally stop shooting anyway
		goto coverfire;

	coverfire:
		#### V 1{
			spread=2;
			A_Coverfire();
			A_SetTics(random(2,6));
		}
		wait;

	aiming.lr:
		#### V 9 {A_FaceLastTargetPos(60);A_Vocalize(activesound);}
		#### V 1 A_StartAim(maxspread:20,maxtics:random(0,35));
		#### V 1 A_LeadTarget(lasttargetdist*0.01,randompick(0,0,0,1));
		//fallthrough to shoot
	shoot.lr:
		#### V 1 A_SetTics(min(1,int(lasttargetdist)>>5));
		#### V 0{
			pitch+=frandom(-spread,spread);
			angle+=frandom(-spread,spread);
		}
	fire.lr:
		#### W 1 bright light("SHOT"){
			if(mag<1){
				setstatelabel("ohforfuckssake");
				return;
			}else{
				A_StartSound("weapons/steyracr/fire",CHAN_WEAPON);
				HDBulletActor.FireBullet(self,"HDB_6mmFlechette",spread:0.95,speedfactor:0.9);
				A_EjectACRCasing();
				pitch+=frandom(-spread,spread*0.5)*0.1;
				angle+=frandom(-spread*0.5,spread)*0.1;
				mag--;}
		}
		#### V 2;
		#### A 0; //A_Jump(128,"fire.lr");
		//fallthrough to postshot
	postshot.lr:
		#### V 12{
			if(!random(0,127))A_Vocalize(activesound);
			if(mag<1){
				setstatelabel("reload");
				return;
			}
			spread=max(0,spread-1);
			A_SetTics(random(2,6));
		}
		#### V 3;
		#### V 0 A_JumpIf(!hdmobai.tryshoot(self),"see");
		#### V 0 A_JumpIfTargetInLOS(1);
		goto coverfire;  //even if not in los,occasionally keep shooting anyway
		#### V 3 A_FaceTarget(10,10);
		#### V 0 A_Jump(50,"see");  //even if in los,occasionally stop shooting anyway
		goto coverfire;
		
	frag:
		#### A 10 A_Vocalize(seesound);
		#### A 20{
			A_StartSound("weapons/pocket",CHAN_WEAPON);
			A_FaceTarget(0,0);
			pitch-=random(10,50);
		}
		#### E 15;
		#### E 10{
			A_SpawnItemEx("HDFragSpoon",cos(pitch)*4,-1,height-6-sin(pitch)*4,cos(pitch)*cos(angle)*4+vel.x,cos(pitch)*sin(angle)*4+vel.y,sin(-pitch)*4+vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			A_ZomFrag();
		}
		#### A 0 A_JumpIf(mag<1,"reload");
		#### A 0 setstatelabel("see");

	jammed:
		#### E 8;
		#### E 0 A_Jump(128,"see");
		#### E 4 A_Vocalize(random(0,2)?seesound:painsound);
		---- A 0 setstatelabel("see");

	ohforfuckssake:
		#### E 8;
	reload:
		---- A 0 A_JumpIf(mag<0,"unloadedreload");
		---- A 6 A_StartSound("weapons/rifleclick2");
		#### AA 1 A_HDChase("melee",null,flags:CHF_FLEE);
		#### A 0{
			A_StartSound("weapons/rifleunload");
			name emptymag="PB_HDSteyrACRMag";
			HDMagAmmo.SpawnMag(self,emptymag,0);
			mag=-1;
		}
	unloadedreload:
		#### BCD 2 A_HDChase("melee",null,flags:CHF_FLEE);
		#### EE random(12,16) A_StartSound("weapons/pocket",8);
		#### E 12 {A_StartSound("weapons/rifleload",9);A_StartSound("weapons/rifleclick2");}
		#### E 8{
			A_StartSound("weapons/rifleclick2",8);
			mag=25;
		}
		#### E 4 {A_StartSound("weapons/steyracr/cock1",CHAN_6,volume:0.6,pitch:0.92);}
		#### E 12 {A_StartSound("weapons/steyracr/cock2",CHAN_6,volume:0.6);}
		#### CCBB 2 A_HDWander();

	see:
		#### A 0{if(firemode>=0)firemode=random(0,2);}
	see2:
		#### A 0 A_JumpIf(!jammed&&mag<1,"reload");
		#### ABCD 4 A_HDChase();
		#### A 0{
			spread=2;
		}
		#### A 0 A_JumpIfTargetInLOS("see");
		#### A 0 A_Jump(24,"roam");
		loop;
	roam:
		#### E 3 A_Jump(60,"roam2");
		#### E 0{spread=1;}
		#### E 4 A_Watch();
		#### E 0{spread=0;}
		#### EEE 4 A_Watch();
		#### A 0 A_Jump(60,"roam");
	roam2:
		#### A 0 A_Jump(8,"see");
		#### ABCD 5 A_HDChase(speedmult:0.6);
		#### A 0 A_Jump(140,"Roam");
		#### A 0 A_JumpIfTargetInLOS("see");
		loop;
	turnaround:
		#### A 0 A_FaceTarget(15,0);
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### E 0{spread=3;}
		#### A 0 A_FaceTarget(15,0);
		#### E 0{spread=6;}
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### E 0{spread=4;}
		#### ABCD 3 A_HDChase();
		---- A 0 setstatelabel("see");
	pain:
		#### G 2;
		#### G 3 A_Vocalize(painsound);
		#### G 0{
			A_ShoutAlert(0.1,SAF_SILENT);
			if(
				floorz==pos.z
				&&target
				&&(
					!random(0,4)
					||distance3d(target)<128
				)
			){
				double ato=angleto(target)+randompick(-90,90);
				vel+=((cos(ato),sin(ato))*speed,1.);
				setstatelabel("missile");
			}
		}
		#### ABCD 2 A_HDChase(flags:CHF_FLEE);
		---- A 0 setstatelabel("see");

	//melee start

	melee:
		#### B 4 A_FaceTarget(0,0);
		#### A 0 A_JumpIf(
			!random(0,15)
			||(
				target
				&&target.pos.z+target.height<pos.z+height*0.6
			)
		,"meleekick");
		#### A 0 A_Jump(256,"meleeheadbig");
	meleekick:
		#### BC 3;
		#### A 6{
			pitch=0;
			A_HumanoidMeleeAttack(frandom(height*0.05,(!!target?target.height:0.4)),1.4);
		}
		#### A 0 setstatelabel("meleeend");
	meleebody:
		#### CD 3;
		#### E 5 A_HumanoidMeleeAttack(height*0.6);
		#### A 0 setstatelabel("meleeend");
	meleehead:
		#### CD 3;
		#### E 5 {
			pitch+=frandom(-40,-8);
			A_HumanoidMeleeAttack(height*0.7);
		}
		#### A 0 setstatelabel("meleeend");
	meleeheadbig:
		#### BC 3;
		#### A 6{
			pitch=0;
			A_HumanoidMeleeAttack(frandom(height*0.05,(!!target?target.height:0.4)),1.4);
		}
		#### CB 3 A_TurnHandDirection(30);
		#### A 8 A_TurnHandDirection(30);
		#### BD 1 A_TurnHandDirection(-30);
		#### V 1 {
			A_TurnHandDirection(-30);
			pitch+=frandom(-45,-8);
			A_HumanoidMeleeAttack(height*0.7,15.);
		}
		#### E 4 A_TurnHandDirection(-10);
		#### A 0 setstatelabel("meleeend");
	meleeend:
		#### D 3 A_JumpIf(
			findstate("missile2",true)
			&&target
			&&!A_JumpIfCloser(64,"null")
		,"meleeendshoot");
		#### A 0 setstatelabel("see");
	meleeendshoot:
		#### E 3;
		#### E 4 A_FaceTarget(10,10);
		#### A 0 setstatelabel("fire");

	//melee end
	falldown:
		#### H 5;
		#### I 5 A_Vocalize(deathsound);
		#### JJKKK 2 A_SetSize(-1,max(deathheight,height-10));
		#### L 0 A_SetSize(-1,deathheight);
		#### L 5;
		#### L 10 A_KnockedDown();
		wait;
	standup:
		#### LK 6;
		#### J 0 A_Jump(160,2);
		#### J 0 A_Vocalize(seesound);
		#### JI 4 A_Recoil(-0.3);
		#### HE 6;
		#### A 0 setstatelabel("see");

	death:
		CRZM H 5;
		CRZM I 5{
			A_Vocalize(deathsound);
		}
		CRZM J 5;
		CRZM KL 5;
	dead:
		CRZM L 1;
		CRZM L 5 canraise{
			if(abs(vel.z)>1)setstatelabel("dead");
		}wait;
	xxxdeath:
		CRZM O 3;
		CRZM P 3{
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
			A_XScream();
		}
		CRZM R 2;
		CRZM QRS 5;
		---- A 0 setstatelabel("gibbed");

	gib:
		CRZM MNO 3;
		CRZM P 3{
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
			A_XScream();
		}
		CRZM R 3 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
		CRZM Q 3;
		CRZM Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
		CRZM RS 3 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160);
	gibbed:
		CRZM T 3;
		CRZM U 5 canraise{
			if(abs(vel.z)>1)setstatelabel("dead");    
		}wait;
	raise:
		CRZM M 2 A_SpawnItemEx("MegaBloodSplatter",0,0,4,0,0,3,0,SXF_NOCHECKPOSITION);
		CRZM ML 6;
		CRZM KJIH 4;
		#### A 0 A_Jump(256,"see");
	ungib:
		CRZM T 6 A_SpawnItemEx("MegaBloodSplatter",0,0,4,0,0,3,0,SXF_NOCHECKPOSITION);
		CRZM TS 12 A_SpawnItemEx("MegaBloodSplatter",0,0,4,0,0,3,0,SXF_NOCHECKPOSITION);
		CRZM RQ 7;
		CRZM POH 5;
		#### A 0 A_Jump(256,"see");
	}
}